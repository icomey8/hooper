hooper {
    Program             = Stmt*
    Stmt                = VarDec
                        | FunctionDec                       
                        | PrintStmt
                        | AssignmentStmt
                        | IfStmt
                        | WhileStmt
                        | break ";"                                         --break  
                        | IncrementStmt
                        | dunk Exp? ";"                                     --dunk
      
    VarDec              = (pick | const) id ":" Type "=" Exp ";"            --withtype
                        | (pick | const) id "=" Exp ";"                     --inference
    FunctionDec         = defineplay id Params ("->" Type)? Block
    Params              = "(" ListOf<Param, ","> ")"
    Param               = id ":" Type

    PrintStmt           = log Exp ";"                   
    AssignmentStmt      = id "=" Exp ";"
    IfStmt              = if Exp Block (putback Block)? (putback IfStmt)? // test `if Exp Block (putback IfStmt | putback Block)?`
    WhileStmt           = while Exp Block
    Block               = "{" Stmt* "}" 
    IncrementStmt       = (Primary_subscript | Primary_id) "++" ";"
    // add decrement
    
    
    FunctionCall        = id "(" ListOf<Exp, ","> ")"
    Type                = "Arr[" Type "]"                                   --array                 
                        | prim_type
    prim_type           = "int" | "boolean" | "string" | "float" 
     
    Exp                 = Exp1

    Exp1                = Exp2 ("or" Exp2)*              --or   
    Exp2                = Exp3 ("and" Exp3)*             --and
    Exp3                = Condition relop Condition      --test
                        | Condition      

    Condition           = Condition "+" Term             --add
                        | Condition "-" Term             --sub
                        | Term

    Term                = Term "*" Factor                   --mult
                        | Term "/" Factor                   --div
                        | Term "%" Factor                   --mod
                        | Factor

    Factor              = Factor "**" Primary               --exp
                        | "-" Primary                       --neg
                        | Primary

    Primary             = true ~mut
                        | false ~mut
                        | numeral
                        | stringlit                         --string
                        | FunctionCall ~mut                 --call
                        | Primary "[" Exp "]"               --subscript
                        | id                                --id
                        | "[[" ListOf<Exp, ","> "]]"        --array
                        | "(" Exp ")"                       --parens
    
    mut                 = ~"==" "=" | "++" | "--"
    pick                = "pick" ~idchar       
    const               = "const" ~idchar      
    log                 = "log" ~idchar
    while               = "while" ~idchar
    if                  = "if" ~idchar          
    putback             = "putback" ~idchar      
    break               = "break" ~idchar
    defineplay          = "define play" ~idchar
    dunk                = "dunk" ~idchar
    true                = "true" ~idchar    
    false               = "false" ~idchar
    keyword             = pick | const | log | while | if | putback | break | defineplay | dunk | true | false
    id                  = ~keyword letter idchar*
    idchar              = letter | digit | "_"
    numeral             = digit+ ("." digit+)? (("E" | "e") ("+"|"-")? digit+)?
    stringlit           = "\"" (~"\"" any)* "\""

    relop               = "<=" | "<" | "==" | "!=" | ">=" | ">"

    space               += "#" (~"\n" any)*          --comment
}